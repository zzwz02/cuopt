/* clang-format off */
/*
 * SPDX-FileCopyrightText: Copyright (c) 2025-2026, NVIDIA CORPORATION & AFFILIATES. All rights reserved.
 * SPDX-License-Identifier: Apache-2.0
 */
/* clang-format on */

#pragma once

#include <dual_simplex/sparse_matrix.hpp>
#include <dual_simplex/types.hpp>

#include <cub/cub.cuh>
#include <rmm/device_scalar.hpp>
#include <rmm/device_vector.hpp>
#include <utilities/copy_helpers.hpp>
#include <utilities/cuda_helpers.cuh>

#include <thrust/device_ptr.h>
#include <thrust/iterator/counting_iterator.h>
#include <thrust/iterator/transform_iterator.h>
#include <thrust/iterator/zip_iterator.h>
#include <thrust/sort.h>
#include <thrust/tabulate.h>
#include <thrust/tuple.h>

namespace cuopt::linear_programming::dual_simplex {

template <typename IndexType, typename ValueType>
class device_csr_matrix_t;

template <typename f_t>
struct sum_reduce_helper_t {
  rmm::device_buffer buffer_data;
  rmm::device_scalar<f_t> out;
  size_t buffer_size;

  sum_reduce_helper_t(rmm::cuda_stream_view stream_view)
    : buffer_data(0, stream_view), out(stream_view)
  {
  }

  template <typename InputIteratorT, typename i_t>
  f_t sum(InputIteratorT input, i_t size, rmm::cuda_stream_view stream_view)
  {
    buffer_size = 0;
    cub::DeviceReduce::Sum(nullptr, buffer_size, input, out.data(), size, stream_view);
    buffer_data.resize(buffer_size, stream_view);
    cub::DeviceReduce::Sum(buffer_data.data(), buffer_size, input, out.data(), size, stream_view);
    return out.value(stream_view);
  }
};

template <typename f_t>
struct transform_reduce_helper_t {
  rmm::device_buffer buffer_data;
  rmm::device_scalar<f_t> out;
  size_t buffer_size;

  transform_reduce_helper_t(rmm::cuda_stream_view stream_view)
    : buffer_data(0, stream_view), out(stream_view)
  {
  }

  template <typename InputIteratorT, typename ReductionOpT, typename TransformOpT, typename i_t>
  f_t transform_reduce(InputIteratorT input,
                       ReductionOpT reduce_op,
                       TransformOpT transform_op,
                       f_t init,
                       i_t size,
                       rmm::cuda_stream_view stream_view)
  {
    auto transformed_input = thrust::make_transform_iterator(input, transform_op);
    cub::DeviceReduce::Reduce(
      nullptr, buffer_size, transformed_input, out.data(), size, reduce_op, init, stream_view);

    buffer_data.resize(buffer_size, stream_view);

    cub::DeviceReduce::Reduce(buffer_data.data(),
                              buffer_size,
                              transformed_input,
                              out.data(),
                              size,
                              reduce_op,
                              init,
                              stream_view);

    return out.value(stream_view);
  }
};

template <typename i_t, typename f_t>
struct csc_view_t {
  raft::device_span<i_t> col_start;
  raft::device_span<i_t> i;
  raft::device_span<f_t> x;
};

template <typename i_t, typename f_t>
class device_csc_matrix_t {
 public:
  device_csc_matrix_t(rmm::cuda_stream_view stream)
    : col_start(0, stream), i(0, stream), x(0, stream), col_index(0, stream)
  {
  }

  device_csc_matrix_t(i_t rows, i_t cols, i_t nz, rmm::cuda_stream_view stream)
    : m(rows),
      n(cols),
      nz_max(nz),
      col_start(cols + 1, stream),
      i(nz_max, stream),
      x(nz_max, stream),
      col_index(0, stream)
  {
  }

  device_csc_matrix_t(device_csc_matrix_t const& other)
    : nz_max(other.nz_max),
      m(other.m),
      n(other.n),
      col_start(other.col_start, other.col_start.stream()),
      i(other.i, other.i.stream()),
      x(other.x, other.x.stream()),
      col_index(other.col_index, other.col_index.stream())
  {
  }

  device_csc_matrix_t(const csc_matrix_t<i_t, f_t>& A, rmm::cuda_stream_view stream)
    : m(A.m),
      n(A.n),
      nz_max(A.col_start[A.n]),
      col_start(A.col_start.size(), stream),
      i(A.i.size(), stream),
      x(A.x.size(), stream)
  {
    col_start = cuopt::device_copy(A.col_start, stream);
    i         = cuopt::device_copy(A.i, stream);
    x         = cuopt::device_copy(A.x, stream);
  }

  void resize_to_nnz(i_t nnz, rmm::cuda_stream_view stream)
  {
    col_start.resize(n + 1, stream);
    i.resize(nnz, stream);
    x.resize(nnz, stream);
    nz_max = nnz;
  }

  csc_matrix_t<i_t, f_t> to_host(rmm::cuda_stream_view stream)
  {
    csc_matrix_t<i_t, f_t> A(m, n, nz_max);
    A.col_start = cuopt::host_copy(col_start, stream);
    A.i         = cuopt::host_copy(i, stream);
    A.x         = cuopt::host_copy(x, stream);
    return A;
  }

  void copy(csc_matrix_t<i_t, f_t>& A, rmm::cuda_stream_view stream)
  {
    m      = A.m;
    n      = A.n;
    nz_max = A.col_start[A.n];
    col_start.resize(A.col_start.size(), stream);
    raft::copy(col_start.data(), A.col_start.data(), A.col_start.size(), stream);
    i.resize(A.i.size(), stream);
    raft::copy(i.data(), A.i.data(), A.i.size(), stream);
    x.resize(A.x.size(), stream);
    raft::copy(x.data(), A.x.data(), A.x.size(), stream);
  }

  /** Same semantics as csc_matrix_t::to_compressed_row, entirely on device. */
  void to_compressed_row(device_csr_matrix_t<i_t, f_t>& Arow, rmm::cuda_stream_view stream) const;

  void form_col_index(rmm::cuda_stream_view stream)
  {
    col_index.resize(x.size(), stream);
    RAFT_CUDA_TRY(cudaMemsetAsync(col_index.data(), 0, sizeof(i_t) * col_index.size(), stream));

    // Scatter 1 when there is a col start in col_index
    if (col_start.size() > 2) {
      thrust::for_each(rmm::exec_policy(stream),
                       thrust::make_counting_iterator(i_t(1)),  // Skip the first 0
                       thrust::make_counting_iterator(
                         static_cast<i_t>(col_start.size() - 1)),  // Skip the end index
                       [span_col_start = cuopt::make_span(col_start),
                        span_col_index = cuopt::make_span(col_index)] __device__(i_t i) {
                         if (span_col_start[i] < span_col_index.size()) {
                           span_col_index[span_col_start[i]] = 1;
                         }
                       });
    }

    // Inclusive cumulative sum to have the corresponding column for each entry
    rmm::device_buffer d_temp_storage;
    size_t temp_storage_bytes{0};
    cub::DeviceScan::InclusiveSum(
      nullptr, temp_storage_bytes, col_index.data(), col_index.data(), col_index.size(), stream);
    d_temp_storage.resize(temp_storage_bytes, stream);
    cub::DeviceScan::InclusiveSum(d_temp_storage.data(),
                                  temp_storage_bytes,
                                  col_index.data(),
                                  col_index.data(),
                                  col_index.size(),
                                  stream);
    // Have to sync since InclusiveSum is being run on local data (d_temp_storage)
    stream.synchronize();
  }

  csc_view_t<i_t, f_t> view()
  {
    csc_view_t<i_t, f_t> v;
    v.col_start = cuopt::make_span(col_start);
    v.i         = cuopt::make_span(i);
    v.x         = cuopt::make_span(x);
    return v;
  }

  i_t nz_max;                          // maximum number of entries
  i_t m;                               // number of rows
  i_t n;                               // number of columns
  rmm::device_uvector<i_t> col_start;  // column pointers (size n + 1)
  rmm::device_uvector<i_t> i;          // row indices, size nz_max
  rmm::device_uvector<f_t> x;          // numerical values, size nz_max
  rmm::device_uvector<i_t> col_index;  // index of each column, only used for scale column
};

template <typename i_t, typename f_t>
class device_csr_matrix_t {
 public:
  device_csr_matrix_t(rmm::cuda_stream_view stream)
    : row_start(0, stream), j(0, stream), x(0, stream)
  {
  }

  device_csr_matrix_t(i_t rows, i_t cols, i_t nz, rmm::cuda_stream_view stream)
    : m(rows),
      n(cols),
      nz_max(nz),
      row_start(rows + 1, stream),
      j(nz_max, stream),
      x(nz_max, stream)
  {
  }

  device_csr_matrix_t(device_csr_matrix_t const& other)
    : nz_max(other.nz_max),
      m(other.m),
      n(other.n),
      row_start(other.row_start, other.row_start.stream()),
      j(other.j, other.j.stream()),
      x(other.x, other.x.stream())
  {
  }

  device_csr_matrix_t(const csr_matrix_t<i_t, f_t>& A, rmm::cuda_stream_view stream)
    : m(A.m),
      n(A.n),
      nz_max(A.row_start[A.m]),
      row_start(A.row_start.size(), stream),
      j(A.j.size(), stream),
      x(A.x.size(), stream)
  {
    row_start = cuopt::device_copy(A.row_start, stream);
    j         = cuopt::device_copy(A.j, stream);
    x         = cuopt::device_copy(A.x, stream);
  }

  void resize_to_nnz(i_t nnz, rmm::cuda_stream_view stream)
  {
    row_start.resize(m + 1, stream);
    j.resize(nnz, stream);
    x.resize(nnz, stream);
    nz_max = nnz;
  }

  csr_matrix_t<i_t, f_t> to_host(rmm::cuda_stream_view stream)
  {
    csr_matrix_t<i_t, f_t> A(m, n, nz_max);
    A.row_start = cuopt::host_copy(row_start, stream);
    A.j         = cuopt::host_copy(j, stream);
    A.x         = cuopt::host_copy(x, stream);
    return A;
  }

  void copy(csr_matrix_t<i_t, f_t>& A, rmm::cuda_stream_view stream)
  {
    m      = A.m;
    n      = A.n;
    nz_max = A.row_start[A.m];
    row_start.resize(A.row_start.size(), stream);
    raft::copy(row_start.data(), A.row_start.data(), A.row_start.size(), stream);
    j.resize(A.j.size(), stream);
    raft::copy(j.data(), A.j.data(), A.j.size(), stream);
    x.resize(A.x.size(), stream);
    raft::copy(x.data(), A.x.data(), A.x.size(), stream);
  }

  i_t nz_max;                          // maximum number of entries
  i_t m;                               // number of rows
  i_t n;                               // number of columns
  rmm::device_uvector<i_t> row_start;  // row pointers (size m + 1)
  rmm::device_uvector<i_t> j;          // column indices, size nz_max
  rmm::device_uvector<f_t> x;          // numerical values, size nz_max

  static_assert(std::is_signed_v<i_t>);  // Require signed integers (we make use of this
                                         // to avoid extra space / computation)
};

template <typename i_t, typename f_t>
void device_csc_matrix_t<i_t, f_t>::to_compressed_row(device_csr_matrix_t<i_t, f_t>& Arow,
                                                      rmm::cuda_stream_view stream) const
{
  static_assert(std::is_signed_v<i_t>);

  // Device CSC -> CSR: col_start[], i[], x[] (this) -> Arow.row_start[], j[], x[].
  // Nonzeros are reordered by sorting (row, col) so each CSR row segment is contiguous.

  i_t const nz = nz_max;

  Arow.m      = m;
  Arow.n      = n;
  Arow.nz_max = nz_max;
  Arow.row_start.resize(m + 1, stream);
  Arow.j.resize(nz, stream);
  Arow.x.resize(nz, stream);

  auto exec = rmm::exec_policy(stream);

  if (nz == 0) {
    // Empty matrix: row_start all zero; j/x unused.
    RAFT_CUDA_TRY(cudaMemsetAsync(Arow.row_start.data(), 0, sizeof(i_t) * (m + 1), stream));
    return;
  }

  // Per-row nnz from CSC row indices i[] (one atomic add per nonzero).
  rmm::device_uvector<i_t> row_counts(m, stream);
  RAFT_CUDA_TRY(cudaMemsetAsync(row_counts.data(), 0, sizeof(i_t) * m, stream));

  thrust::for_each(exec,
                   thrust::make_counting_iterator<i_t>(0),
                   thrust::make_counting_iterator<i_t>(nz),
                   [row_ind = i.data(), counts = row_counts.data()] __device__(i_t p) {
                     atomicAdd(counts + row_ind[p], i_t(1));
                   });

  // CSR row pointers: exclusive prefix sum of row_counts; Arow.row_start[m] = nz.
  rmm::device_buffer scan_tmp;
  std::size_t scan_bytes = 0;
  cub::DeviceScan::ExclusiveSum(
    nullptr, scan_bytes, row_counts.data(), Arow.row_start.data(), m, stream);
  scan_tmp.resize(scan_bytes, stream);
  cub::DeviceScan::ExclusiveSum(
    scan_tmp.data(), scan_bytes, row_counts.data(), Arow.row_start.data(), m, stream);

  RAFT_CUDA_TRY(
    cudaMemcpyAsync(Arow.row_start.data() + m, &nz, sizeof(i_t), cudaMemcpyHostToDevice, stream));

  // rows[]: CSC row indices (sort key). Arow.j / Arow.x hold (col, val) per flat CSC index,
  // then sort_by_key permutes j and x in place into CSR (row, col) order.
  rmm::device_uvector<i_t> rows(nz, stream);
  raft::copy(rows.data(), i.data(), nz, stream);
  raft::copy(Arow.x.data(), x.data(), nz, stream);

  // Global CSC position p lies in column c iff col_start[c] <= p < col_start[c+1].
  thrust::tabulate(exec,
                   thrust::device_pointer_cast(Arow.j.data()),
                   thrust::device_pointer_cast(Arow.j.data() + nz),
                   [cs = col_start.data(), nn_c = n] __device__(i_t p) {
                     i_t lo = 0;
                     i_t hi = nn_c;
                     while (lo < hi) {
                       i_t mid = lo + (hi - lo) / 2;
                       if (cs[mid] <= p) {
                         lo = mid + 1;
                       } else {
                         hi = mid;
                       }
                     }
                     return lo - 1;
                   });

  // CSR column order: sort (row, col) lexicographically; values follow the same permutation.
  auto row_iter = thrust::device_pointer_cast(rows.data());
  auto col_iter = thrust::device_pointer_cast(Arow.j.data());
  thrust::sort_by_key(exec,
                      thrust::make_zip_iterator(thrust::make_tuple(row_iter, col_iter)),
                      thrust::make_zip_iterator(thrust::make_tuple(row_iter + nz, col_iter + nz)),
                      thrust::device_pointer_cast(Arow.x.data()));
}

}  // namespace cuopt::linear_programming::dual_simplex
